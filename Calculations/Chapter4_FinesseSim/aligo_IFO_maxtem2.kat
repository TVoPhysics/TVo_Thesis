#--------------------------------------------------------------------------
# aligo_IFO_maxtem2.kat (Mode matching optimized for ~18W input power)
#
# FINESSE kat file for aligo dual recycled michelson with FP arm cavities
# Tuned for use with maxtem 2 option.
#
# Parameters are mostly design parameters (see below).
#
# To be used as a base file for investigations into active wavefront control
# requirements, sensing and actuation. Compatible with "awc_tools.py" file 
# for performing automated tasks (zero locks, plot error signals etc.)
# 
#  > This is a file based on aligo_central_IFO.kat where the following
#    features have been added:
#  - Thermal lensing at ITMs (modeled as a thin lens outside
#    the substrate). Default value is 34.5km, corresponding to the lens 
#    predicted at ~18W input power with 0.5ppm absorption on ITMs.
#  - SRM thickness;
#  - ROCs and lengths of PRC SRC changed following T0900043 (Table 2) to 
#    mode match the cavities with a 34.5km thermal lens in ITMs.
#  - However, PR3 to BS and SR3 to BS are adjusted to maintain overall 
#    quoted P/SRC length. These lengths have minimal effect on mode matching.
#  - N.B. the thermal lens quoted in T0900043 is 50km, but this was 
#    modeled as being inside the ITM substrate, so in this model the focal 
#    length is adjusted by a factor 1/nsilica. 
#
# FINESSE kat file for aLIGO dual recycled michelson with FP arm cavities.
#
# Lengths and Rcs 
# - generally taken from design document T0900043-11 unless otherwise 
#   stated 
# - Schnupp asymmetry changed to 8cm (from 5cm) 
# - Average distance between BS and ITMX and ITMY calculated using length 
#   of PRC, as not explicitly given (then +/- 4cm on distances to give 
#   Schnupp asymmetry)
# Reflectivities/transmission/losses
# - also from T0900043-11
# - transmission of SRM changed to 35% (i.e. see T1300507-v1)
# - losses at each mirror = 37.5ppm 
#
# Also use T1000298-T for parameters, (design) control scheme with some 
# updates to match current sensing scheme.
# 
# https://dcc.ligo.org/LIGO-L1300231 
# https://awiki.ligo-wa.caltech.edu/aLIGO/Finesse%20for%20aLIGO
# https://alog.ligo-la.caltech.edu/aLOG/index.php?callRep=8102
#
# DARM offset adjusted to give 147mW DC offset at 125W input power
# DARM error signal derived from OMC DC power
#
# Charlotte Bond, Daniel Brown, Paul Fulda, Antonio Perreca, Andreas Freise 2015-09-08
#--------------------------------------------------------------------------

%%% FTblock laser
###########################################################################

l L0 125 0 n0
s lmod1 1 n0 n1
mod mod1 $f1 0.18 1 pm n1 n2
s lmod2 1 n2 n3
mod mod2 $f2 0.18 1 pm n3 n4
s lmod3 1 n4 nMC1in

###########################################################################
%%% FTend Laser

%%% FTblock IMC
#####################################################################################
cav cavIMC MC2 nMC2in MC2 nMC2refl 

bs1 MC1 6030u 0.6u 0 44.59 nMC1in nMC1refl nMC1trans nMC1fromMC3 
s sMC1toMC2 16.2405708 nMC1trans nMC2in 

bs1 MC2 5.1u 9.3u 0 0.82 nMC2in nMC2refl nMC2trans dump1
s sMC2toMC3 16.2405708 nMC2refl nMC3in 
attr MC2 Rc 27.275

bs1 MC3 5845u 0.8u 0 44.59 nMC3in nMC3refl nMC3trans nMCreturn_refl
s sMC3toMC1 0.465 nMC3refl nMC1fromMC3

s sMC3substrate 0.0845 $nsilica nMC3trans nMC3ARin
bs2 MC3AR 0 0 0 28.9661 nMC3ARin dump nMC3ARtrans dump
s sMC3ARtoIM1 0.4282 nMC3ARtrans nIM1in

#####################################################################################
%%% FTend IMC

%%% FTblock HAM2
#####################################################################################

# IM1 a.k.a. SM1
bs1 IM1 0 0 0 53 nIM1in nIM1refl nIM1HRtrans dump
s sIM1sub 0.02995 $nsilica nIM1HRtrans nIM1ARin
bs2 IM1AR 0 0 0 33.4 nIM1ARin dump nIM1ARtrans dump

s IM1AR_nanoscan 3 nIM1ARtrans nIOT_Lnanoscan

#AOE1
s sIM1_AOE1 0.1955 nIM1refl nAOE1in
lens AOE1 inf nAOE1in nAOE1trans

# IM2 a.k.a. PMMT1 
s sAOE1_IM2 1.0983 nAOE1trans nIM2in
bs1 IM2 0 0 0 7 nIM2in nIM2refl dump dump
attr IM2 Rc 12.8 

s sIM2toIM3 1.1704 nIM2refl nIM3in

# IM3 a.k.a PMMT2
#s sIM2_IM3 1.1704 nIM2refl nIM3in
bs1 IM3 0 0 0 7.1 nIM3in nIM3refl dump dump
attr IM3 Rc -6.24

#AOE2
s sIM3_AOE2 1.041 nIM3refl nAOE2in
lens AOE2 inf nAOE2in nAOE2trans

# IM4 a.k.a. SM2
s sAOE2_IM4 0.134 nAOE2trans nIM4in

bs1 IM4 2400u 0 0 45 nIM4in nIM4refl nIM4trans nIM4rettrans
s sIM4toREFLpick 0.4135 nIM4refl nREFLpickin

bs REFLpick 0.001 0.999 0 45 nREFLpickin nREFLrefl nREFLtrans nREFLretrefl
s sREFLpicktoPRMAR 0 nREFLtrans nREFL

#####################################################################################
%%% FTend HAM2

%%% FTblock PR
###########################################################################

m2 PRMAR 35u 4.5u $phi_PRM nREFL nPRMARb     # AR surface
s sPRMsub1 0.0737 $nsilica nPRMARb nPRMHRa   # Substrate
m1 PRMHR 0.03 8.5u $phi_PRM nPRMHRa nPRMHRb  # HR surface
attr PRMHR Rc 11.009

s lp1 $Lpr1 nPRMHRb nPR2a 
bs1 PR2 250u $Mloss 0 -0.79 nPR2a nPR2b nPOP dump
s lp2 $Lpr2 nPR2b nPR3a
bs1 PR3 0 $Mloss 0 0.615 nPR3a nPR3b dump dump
s lp3 $Lpr3 nPR3b nPRBS

attr PR2 Rc -4.545
attr PR3 Rc 36.027

###########################################################################
%%% FTend PR

%%% FTblock BS
###########################################################################

# BS beamsplitter
##------------------------------------------------------------ 
## BS 
##                             ^ 
##                  to IMY     |      
##                             |      ,'-.
##                             |     +    `. 
##                        nYBS |   ,'       :'
##      nPR3b                  |  +i1      +
##         ---------------->    ,:._  i2 ,'
##    from the PRC       nPRBS + \  `-. + nXBS
##                           ,' i3\   ,' --------------->
##                          +      \ +     to IMX
##                        ,'     i4.'
##                       `._      ..
##                          `._ ,' |nSRBS
##                             -   |
##                                 |to the SRC
##                                 |
##                                 v
##------------------------------------------------------------

bs1 BS 0.5 $Mloss $phi_BS 45 nPRBS nYBS nBSi1 nBSi3
s BSsub1 0.0685 $nsilica nBSi1 nBSi2
s BSsub2 0.0684 $nsilica nBSi3 nBSi4
bs2 BSAR1 50u 0 0 -29.195 nBSi2 dump nXBS nPOX
bs2 BSAR2 50u 0 0 29.195 nBSi4 dump nSRBS dump

###########################################################################
%%% FTend BS

%%% FTblock Yarm
###########################################################################

s ly1 5.01258 nYBS nITMY11   # Distance from beam splitter to Y arm input mirror          

# Thermal lens correction
lens ITMYTL $TL_f nITMY11 nITMYTLtrans
s ITMYTL_null 0 nITMYTLtrans nITMYconstL_in
lens ITMYconstL inf nITMYconstL_in nITMYconstL_trans
s ITMYTL_null2 0 nITMYconstL_trans nITMY1

# Y arm input mirror
m2 ITMYAR 20u 0 $phi_ITMY nITMY1 nITMYs1
s ITMYsub 0.2 $nsilica nITMYs1 nITMYs2
m1 ITMYHR 0.014 $Mloss $phi_ITMY nITMYs2 nITMY2

s LYarm 3994.5 nITMY2 nETMY1   # Y arm length

# Y arm end mirror
m1 ETMYHR 5u $Mloss $phi_ETMY nETMY1 nETMYs1

attr ITMYHR Rc -1934
attr ETMYHR Rc 2245

attr ITMYHR mass 40
attr ETMYHR mass 40

###########################################################################
%%% FTend Yarm

%%% FTblock Xarm
###########################################################################

# Distance from beam splitter to X arm input mirror 
s lx1 4.99294 nXBS nITMX11

# Thermal lens correction
lens ITMXTL $TL_f nITMX11 nITMXTLtrans
s ITMXtl_null 0 nITMXTLtrans nITMXconstL_in
lens ITMXconstL inf nITMXconstL_in nITMXconstL_trans
s ITMXTL_null2 0 nITMXconstL_trans nITMX1

# X arm input mirror
m2 ITMXAR 20u 0 $phi_ITMX nITMX1 nITMXs1
s ITMXsub 0.2 $nsilica nITMXs1 nITMXs2
m1 ITMXHR 0.014 $Mloss $phi_ITMX nITMXs2 nITMX2
#m1 ITMXHR 0 $Mloss $phi_ITMX nITMXs2 nITMX2

# X arm length
s LXarm 3994.5 nITMX2 nETMX1

# X arm end mirror
m1 ETMXHR 5u $Mloss $phi_ETMX nETMX1 nETMXs1

attr ITMXHR Rc -1934
attr ETMXHR Rc 2245

attr ETMXHR mass 40
attr ITMXHR mass 40

###########################################################################
%%% FTend Xarm

%%% FTblock SR
###########################################################################

s ls3 $Lsr3 nSRBS nSR3b

bs1 SR3 0 $Mloss 0 0.785 nSR3b nSR3a dump17 dump
attr SR3 Rc 35.972841

s ls2 $Lsr2 nSR3a nSR2b

bs1 SR2 0 $Mloss 0 -0.87 nSR2b nSR2a dump dump
attr SR2 Rc -6.406

s ls1 $Lsr1 nSR2a nSRMHRa

m1 SRMHR $T_SRM $L_SRM $phi_SRM nSRMHRa nSRMHRb 
s SRMsub 0.0749 $nsilica nSRMHRb nSRMARa
m2 SRMAR 50n 0 $phi_SRM nSRMARa nSRMARb

attr SRMHR Rc -5.6938

###########################################################################
%%% FTend SR

%%% FTblock OMCpath
###########################################################################
# (Locations and angles based on the solid work file D1000342-v14 give ~5% 
# mismatch. Thus SRM_OM1, OM3_OMC have been adjusted to get ~99.7% overlap at the OMC)
# (SRM_OM1=2.6334, OM3_OMC=0.24.8 give 99% overlap at OMC)

s sSRM_OM1 3.66164 nSRMARb nOM1a                         # extra length takes into account faraday isolator path

bs1 OM1 800u $Mloss 0 2.251 nOM1a nOM1b dump dump        # T is set for high power; Loss is a guess
attr OM1 Rc 4.6

s sOM1_OM2 1.395 nOM1b nOM2a 

bs1 OM2 10u $Mloss 0 4.399 nOM2a nOM2b dump dump         # T is a guess
attr OM2 Rc 1.7058

s sOM2_OM3 0.631 nOM2b nOM3a 

bs1 OM3 0.01 $Mloss 0 30.037 nOM3a nOM3b nOM3trans dump  # T is a from T1200410-v2

s sOM3_OMC 0.196 nOM3b nOMCin                            # By design should be ~0.31

###########################################################################
%%% FTend OMCpath 

%%% FTblock OMC
###########################################################################
# Output mode cleaner (as built parameters: D1300507-v1)

bs1 OMC1 0.0076 10u 0 4.004 nOMCin nOMCrefl nOMC1b nOMC1a    # Input mirror IC (flat mirror)

s sOMC1_OMC2 0.2815 1 nOMC1b nOMC2a                          # OMC1 -> OMC2

bs1 OMC2 0.0075 10u 0 4.004 nOMC2b nOMC2a dump nOMCout       # Output Coupler OC (flat mirror)

s sOMC2_OMC3 0.2842 1 nOMC2b nOMC3a                          # Distance from OC to CM1

bs1 OMC3 36u 10u 0 4.004 nOMC3a nOMC3b dump dump             # Curved Mirror CM1
attr OMC3 Rc 2.57321

s sOMC3_OMC4 0.2815 1 nOMC3b nOMC4a                          # Distance from CM1 to CM2 

bs1 OMC4 35.9u 10u 0 4.004 nOMC4a nOMC4b dump dump           # Curved Mirror CM2
attr OMC4 Rc 2.57369

s sOMC4_OMC1 0.2842 1 nOMC4b nOMC1a                          # Distance from CM2 to IC 

###########################################################################
%%% FTend OMC

%%% FTblock Lengths
###########################################################################

const Lpr1 16.6107
const Lpr2 16.1647
const Lpr3 19.5381 
const Lsr1 15.7586
const Lsr2 15.4435
const Lsr3 19.3661

###########################################################################
%%% FTend Lengths

%%% FTblock Tunings
###########################################################################
const phi_SRM 90.0079349663405
const phi_PRM 0.000176445971388508
const phi_ITMX 4.60943808179651e-05
const phi_ITMY -4.60943808179651e-05
const phi_ETMX 0.00172729497882701
const phi_ETMY -0.00176526469459912
const phi_BS 0
###########################################################################
%%% FTend Tunings

%%% FTblock Constants
###########################################################################
const Mloss 37.5u
const T_SRM 0.35
const L_SRM 8.7u

const nsilica 1.44963098985906
const nTGG 1.954
const nCalcite 1.65846
const f1 9099471
const f1h2 18198942
const nf1 -9099471
const nf1h2 -18198942
const f2 45497355
const f2h2 90994710
const nf2 -45497355
const nf2h2 -90994710

const f3 24000000
const nf3 -24000000

const fM 36397884
const nfM -36397884
const fP 54596826
const TL_f 34.5k

const DARM_DC_offset 0.147303
###########################################################################
%%% FTend Constants

%%% FTblock errsigs
###########################################################################

pd1 REFL_f1_I $f1 101 nREFL
pd1 REFL_f1_Q $f1 191 nREFL
pd1 REFL_f2_I $f2 14 nREFL
pd1 REFL_f2_Q $f2 104 nREFL
pd1 POP_f1_I $f1 101 nPOP
pd1 POP_f1_Q $f1 191 nPOP
pd1 POP_f2_I $f2 13 nPOP
pd1 POP_f2_Q $f2 103 nPOP
pd1 AS_f1_I $f1 0 nSRMARb
pd1 AS_f1_Q $f1 90 nSRMARb
pd1 AS_f2_I $f2 13 nSRMARb
pd1 AS_f2_Q $f2 103 nSRMARb

###########################################################################
%%% FTend errsigs

%%% FTblock Powers
###########################################################################

pd P_DC_AS  nSRMARb
pd P_DC_OMC nOMCout
pd PIMCtrans nREFL*
pd Px nITMX2
pd Py nITMY2
pd Pprc nPRMHRb
pd Psrc nSRMHRa*
ad prc0 0 nPRMHRb
ad prcf1 $f1 nPRMHRb
ad prcf2 $f2 nPRMHRb

ad aoc0 0 nOMCout
ad asc0 0 nSRMARb
ad asf1 $f1 nSRMARb
ad asf2 $f2 nSRMARb
ad src0 0 nSRMHRa*
ad srcf1 $f1 nSRMHRa*
ad srcf2 $f2 nSRMHRa*

###########################################################################
%%% FTend Powers

%%% FTblock HOMs
###########################################################################

cav cavYARM ITMYHR nITMY2 ETMYHR nETMY1
cav cavXARM ITMXHR nITMX2 ETMXHR nETMX1
cav cavSRX SRMHR nSRMHRa ITMXHR nITMXs2
cav cavSRY SRMHR nSRMHRa ITMYHR nITMYs2
cav cavPRX PRMHR nPRMHRb ITMXHR nITMXs2
cav cavPRY PRMHR nPRMHRb ITMYHR nITMYs2
cav cavOMC OMC2 nOMC2b OMC2 nOMC2a
maxtem 2

###########################################################################
%%% FTend HOMs

%%% FTblock locks
###########################################################################

set PRCL_err POP_f1_I  re
set MICH_err POP_f2_Q  re
set CARM_err REFL_f1_I re
set SRCL_err REFL_f2_I re
set OMC_DC P_DC_OMC re

func DARM_err = $OMC_DC - $DARM_DC_offset

lock PRCL_lock $PRCL_err -2.76303399282 10u
lock MICH_lock $MICH_err 12.0430862067 10u
lock CARM_lock $CARM_err 0.000118839751146 10u
lock DARM_lock $DARM_err -0.00578107746673 1u
lock SRCL_lock $SRCL_err -4.90256095433 10u

func mMICH_lock = 0 - $MICH_lock
func ETMX_lock  = $CARM_lock + $MICH_lock + $DARM_lock
func ETMY_lock  = $CARM_lock - $MICH_lock - $DARM_lock

put* PRMHR   phi     $PRCL_lock
put* PRMAR   phi     $PRCL_lock

put* ITMXHR  phi     $MICH_lock
put* ITMXAR  phi     $MICH_lock
put* ITMYHR  phi     $mMICH_lock
put* ITMYAR  phi     $mMICH_lock

put* ETMXHR  phi     $ETMX_lock

put* ETMYHR  phi     $ETMY_lock

put* SRMHR   phi     $SRCL_lock
put* SRMAR   phi     $SRCL_lock

noplot PRCL_lock
noplot SRCL_lock
noplot MICH_lock
noplot DARM_lock
noplot CARM_lock
noplot mMICH_lock
noplot ETMX_lock
noplot ETMY_lock

###########################################################################
%%% FTend locks

%%% FTblock ASWFS
###########################################################################
s lOM3trans2ASWFSBS 0.66 nOM3trans nASWFSBSin
bs ASWFSBS 0.5 0.5 0 45 nASWFSBSin nASWFSBSrefl nASWFSBStrans dump

s lASWFSBS2ASWFSA 0.1 nASWFSBSrefl nASWFSA
#attr lASWFSBS2ASWFSA gouy 70

# lens and path tuned to give default 90deg Gouy phase difference between WFSA and WFSB
s lASWFSBS2ASWFSBlens 0.379 nASWFSBStrans nASWFSBlensin
lens ASWFSblens 0.25 nASWFSBlensin nASWFSBlensout
s lASWFSblens2WFSBsteer 0 nASWFSBlensout nASWFSBsteerin
bs2 ASWFSBsteer 1 0 0 45 nASWFSBsteerin nASWFSBsteerrefl dump dump
s lASWFSBlens2ASWFSB 0.2878 nASWFSBsteerrefl nASWFSB
#attr lASWFSBS2ASWFSB gouy 160

# AS WFS RF detectors

pd1 ASWFSA_18I_P $f1h2 $ASWFSA18Idemod nASWFSA
pd1 ASWFSA_18Q_P $f1h2 $ASWFSA18Qdemod nASWFSA

pd1 ASWFSA_36I_P $fM $ASWFSA36Idemod nASWFSA
pd1 ASWFSA_36Q_P $fM $ASWFSA36Qdemod nASWFSA

pd1 ASWFSA_45I_P $f2 0 nASWFSA
pd1 ASWFSA_45Q_P $f2 90 nASWFSA

pd1 ASWFSA_90I_P $f2h2 $ASWFSA90Idemod nASWFSA
pd1 ASWFSA_90Q_P $f2h2 $ASWFSA90Qdemod nASWFSA

pdtype ASWFSA_45I_P y-split
pdtype ASWFSA_45Q_P y-split
pdtype ASWFSA_36I_P y-split
pdtype ASWFSA_36Q_P y-split
pdtype ASWFSA_18I_P y-split
pdtype ASWFSA_18Q_P y-split
pdtype ASWFSA_90I_P y-split
pdtype ASWFSA_90Q_P y-split

pd1 ASWFSB_18I_P $f1h2 $ASWFSB18Idemod nASWFSB
pd1 ASWFSB_18Q_P $f1h2 $ASWFSB18Qdemod nASWFSB

pd1 ASWFSB_36I_P $fM $ASWFSB36Idemod nASWFSB
pd1 ASWFSB_36Q_P $fM $ASWFSB36Qdemod nASWFSB

pd1 ASWFSB_45I_P $f2 0 nASWFSB
pd1 ASWFSB_45Q_P $f2 90 nASWFSB

pd1 ASWFSB_90I_P $f2h2 $ASWFSB90Idemod nASWFSB
pd1 ASWFSB_90Q_P $f2h2 $ASWFSB90Qdemod nASWFSB

pdtype ASWFSB_45I_P y-split
pdtype ASWFSB_45Q_P y-split
pdtype ASWFSB_36I_P y-split
pdtype ASWFSB_36Q_P y-split
pdtype ASWFSB_18I_P y-split
pdtype ASWFSB_18Q_P y-split
pdtype ASWFSB_90I_P y-split
pdtype ASWFSB_90Q_P y-split

pd1 ASWFSA_18I_Y $f1h2 $ASWFSA18Idemod nASWFSA
pd1 ASWFSA_18Q_Y $f1h2 $ASWFSA18Qdemod nASWFSA

pd1 ASWFSA_36I_Y $fM $ASWFSA36Idemod nASWFSA
pd1 ASWFSA_36Q_Y $fM $ASWFSA36Qdemod nASWFSA

pd1 ASWFSA_45I_Y $f2 0 nASWFSA
pd1 ASWFSA_45Q_Y $f2 90 nASWFSA

pd1 ASWFSA_90I_Y $f2h2 $ASWFSA90Idemod nASWFSA
pd1 ASWFSA_90Q_Y $f2h2 $ASWFSA90Qdemod nASWFSA

pdtype ASWFSA_45I_Y x-split
pdtype ASWFSA_45Q_Y x-split
pdtype ASWFSA_36I_Y x-split
pdtype ASWFSA_36Q_Y x-split
pdtype ASWFSA_18I_Y x-split
pdtype ASWFSA_18Q_Y x-split
pdtype ASWFSA_90I_Y x-split
pdtype ASWFSA_90Q_Y x-split

pd1 ASWFSB_18I_Y $f1h2 $ASWFSB18Idemod nASWFSB
pd1 ASWFSB_18Q_Y $f1h2 $ASWFSB18Qdemod nASWFSB

pd1 ASWFSB_36I_Y $fM $ASWFSB36Idemod nASWFSB
pd1 ASWFSB_36Q_Y $fM $ASWFSB36Qdemod nASWFSB

pd1 ASWFSB_45I_Y $f2 0 nASWFSB
pd1 ASWFSB_45Q_Y $f2 90 nASWFSB

pd1 ASWFSB_90I_Y $f2h2 $ASWFSB90Idemod nASWFSB
pd1 ASWFSB_90Q_Y $f2h2 $ASWFSB90Qdemod nASWFSB

pdtype ASWFSB_45I_Y x-split
pdtype ASWFSB_45Q_Y x-split
pdtype ASWFSB_36I_Y x-split
pdtype ASWFSB_36Q_Y x-split
pdtype ASWFSB_18I_Y x-split
pdtype ASWFSB_18Q_Y x-split
pdtype ASWFSB_90I_Y x-split
pdtype ASWFSB_90Q_Y x-split

const fASC 1

# AS WFS RF detectors double demodulated at signal frequency $fASC

pd2 ASWFSA_18I_audio_P $f1h2 $ASWFSA18Idemod $fASC nASWFSA
pd2 ASWFSA_18Q_audio_P $f1h2 $ASWFSA18Qdemod $fASC nASWFSA

pd2 ASWFSA_36I_audio_P $fM $ASWFSA36Idemod $fASC nASWFSA
pd2 ASWFSA_36Q_audio_P $fM $ASWFSA36Qdemod $fASC nASWFSA

pd2 ASWFSA_45I_audio_P $f2 0 $fASC nASWFSA
pd2 ASWFSA_45Q_audio_P $f2 90 $fASC nASWFSA

pd2 ASWFSA_90I_audio_P $f2h2 $ASWFSA90Idemod $fASC nASWFSA
pd2 ASWFSA_90Q_audio_P $f2h2 $ASWFSA90Qdemod $fASC nASWFSA

pdtype ASWFSA_45I_audio_P y-split
pdtype ASWFSA_45Q_audio_P y-split
pdtype ASWFSA_36I_audio_P y-split
pdtype ASWFSA_36Q_audio_P y-split
pdtype ASWFSA_18I_audio_P y-split
pdtype ASWFSA_18Q_audio_P y-split
pdtype ASWFSA_90I_audio_P y-split
pdtype ASWFSA_90Q_audio_P y-split

pd2 ASWFSB_18I_audio_P $f1h2 $ASWFSB18Idemod $fASC nASWFSB
pd2 ASWFSB_18Q_audio_P $f1h2 $ASWFSB18Qdemod $fASC nASWFSB

pd2 ASWFSB_36I_audio_P $fM $ASWFSB36Idemod $fASC nASWFSB
pd2 ASWFSB_36Q_audio_P $fM $ASWFSB36Qdemod $fASC nASWFSB

pd2 ASWFSB_45I_audio_P $f2 0 $fASC nASWFSB
pd2 ASWFSB_45Q_audio_P $f2 90 $fASC nASWFSB

pd2 ASWFSB_90I_audio_P $f2h2 $ASWFSB90Idemod $fASC nASWFSB
pd2 ASWFSB_90Q_audio_P $f2h2 $ASWFSB90Qdemod $fASC nASWFSB


pdtype ASWFSB_45I_audio_P y-split
pdtype ASWFSB_45Q_audio_P y-split
pdtype ASWFSB_36I_audio_P y-split
pdtype ASWFSB_36Q_audio_P y-split
pdtype ASWFSB_18I_audio_P y-split
pdtype ASWFSB_18Q_audio_P y-split
pdtype ASWFSB_90I_audio_P y-split
pdtype ASWFSB_90Q_audio_P y-split

pd2 ASWFSA_18I_audio_Y $f1h2 $ASWFSA18Idemod $fASC nASWFSA
pd2 ASWFSA_18Q_audio_Y $f1h2 $ASWFSA18Qdemod $fASC nASWFSA

pd2 ASWFSA_36I_audio_Y $fM $ASWFSA36Idemod $fASC nASWFSA
pd2 ASWFSA_36Q_audio_Y $fM $ASWFSA36Qdemod $fASC nASWFSA

pd2 ASWFSA_45I_audio_Y $f2 0 $fASC nASWFSA
pd2 ASWFSA_45Q_audio_Y $f2 90 $fASC nASWFSA

pd2 ASWFSA_90I_audio_Y $f2h2 $ASWFSA90Idemod $fASC nASWFSA
pd2 ASWFSA_90Q_audio_Y $f2h2 $ASWFSA90Qdemod $fASC nASWFSA

pdtype ASWFSA_45I_audio_Y x-split
pdtype ASWFSA_45Q_audio_Y x-split
pdtype ASWFSA_36I_audio_Y x-split
pdtype ASWFSA_36Q_audio_Y x-split
pdtype ASWFSA_18I_audio_Y x-split
pdtype ASWFSA_18Q_audio_Y x-split
pdtype ASWFSA_90I_audio_Y x-split
pdtype ASWFSA_90Q_audio_Y x-split

pd2 ASWFSB_18I_audio_Y $f1h2 $ASWFSB18Idemod $fASC nASWFSB
pd2 ASWFSB_18Q_audio_Y $f1h2 $ASWFSB18Qdemod $fASC nASWFSB

pd2 ASWFSB_45I_audio_Y $f2 0 $fASC nASWFSB
pd2 ASWFSB_45Q_audio_Y $f2 90 $fASC nASWFSB

pd2 ASWFSB_36I_audio_Y $fM $ASWFSB36Idemod $fASC nASWFSB
pd2 ASWFSB_36Q_audio_Y $fM $ASWFSB36Qdemod $fASC nASWFSB

pd2 ASWFSB_90I_audio_Y $f2h2 $ASWFSB90Idemod $fASC nASWFSB
pd2 ASWFSB_90Q_audio_Y $f2h2 $ASWFSB90Qdemod $fASC nASWFSB

pdtype ASWFSB_45I_audio_Y x-split
pdtype ASWFSB_45Q_audio_Y x-split
pdtype ASWFSB_36I_audio_Y x-split
pdtype ASWFSB_36Q_audio_Y x-split
pdtype ASWFSB_18I_audio_Y x-split
pdtype ASWFSB_18Q_audio_Y x-split
pdtype ASWFSB_90I_audio_Y x-split
pdtype ASWFSB_90Q_audio_Y x-split

pd1 ASWFSA18sum $f1h2 nASWFSA
pd1 ASWFSB18sum $f1h2 nASWFSB

pd1 ASWFSA36sum $fM nASWFSA
pd1 ASWFSB36sum $fM nASWFSB

pd1 ASWFSA90sum $f2h2 nASWFSA
pd1 ASWFSB90sum $f2h2 nASWFSB

const ASWFSA18Idemod 240.66546
const ASWFSA18Qdemod 150.66546
const ASWFSB18Idemod 228.2786983
const ASWFSB18Qdemod 138.2786983

const ASWFSA36Idemod 252.9610861
const ASWFSA36Qdemod 162.9610861
const ASWFSB36Idemod 228.1875629
const ASWFSB36Qdemod 138.1875629

const ASWFSA90Idemod 26.58349357
const ASWFSA90Qdemod 116.58349357
const ASWFSB90Idemod 324.6496854
const ASWFSB90Qdemod 234.6496854

###########################################################################
%%% FTend ASWFS

%%% FTblock REFLWFS
###########################################################################

s sREFLpicktoREFLWFSBS 0 nREFLretrefl nREFLWFSBSin
bs REFLWFSBS 0.5 0.5 0 45 nREFLWFSBSin nREFLWFSBSrefl nREFLWFSA dump
s sREFLWFSBStoREFLWFSBlens 0 nREFLWFSBSrefl nREFLWFSBlensin
lens REFLWFSBlens 1 nREFLWFSBlensin nREFLWFSBlenstrans
s sREFLWFSBlenstoREFLWFSB 0.88264 nREFLWFSBlenstrans nREFLWFSB

pd1 REFLWFSA_45I_Y $f2 16.06 nREFLWFSA
pd1 REFLWFSA_45Q_Y $f2 106.06 nREFLWFSA
pd1 REFLWFSB_45I_Y $f2 327.83 nREFLWFSB
pd1 REFLWFSB_45Q_Y $f2 57.83 nREFLWFSB

pdtype REFLWFSA_45I_Y x-split
pdtype REFLWFSA_45Q_Y x-split
pdtype REFLWFSB_45I_Y x-split
pdtype REFLWFSB_45Q_Y x-split

pd1 REFLWFSA_45I_P $f2 16.06 nREFLWFSA
pd1 REFLWFSA_45Q_P $f2 106.06 nREFLWFSA
pd1 REFLWFSB_45I_P $f2 327.83 nREFLWFSB
pd1 REFLWFSB_45Q_P $f2 57.83 nREFLWFSB

pdtype REFLWFSA_45I_P y-split
pdtype REFLWFSA_45Q_P y-split
pdtype REFLWFSB_45I_P y-split
pdtype REFLWFSB_45Q_P y-split

pd2 REFLWFSA_45I_audio_Y $f2 16.06 $fASC nREFLWFSA
pd2 REFLWFSA_45Q_audio_Y $f2 106.06 $fASC nREFLWFSA
pd2 REFLWFSB_45I_audio_Y $f2 327.83 $fASC nREFLWFSB
pd2 REFLWFSB_45Q_audio_Y $f2 57.83 $fASC nREFLWFSB

pdtype REFLWFSA_45I_audio_Y x-split
pdtype REFLWFSA_45Q_audio_Y x-split
pdtype REFLWFSB_45I_audio_Y x-split
pdtype REFLWFSB_45Q_audio_Y x-split

pd2 REFLWFSA_45I_audio_P $f2 16.06 $fASC nREFLWFSA
pd2 REFLWFSA_45Q_audio_P $f2 106.06 $fASC nREFLWFSA
pd2 REFLWFSB_45I_audio_P $f2 327.83 $fASC nREFLWFSB
pd2 REFLWFSB_45Q_audio_P $f2 57.83 $fASC nREFLWFSB

pdtype REFLWFSA_45I_audio_P y-split
pdtype REFLWFSA_45Q_audio_P y-split
pdtype REFLWFSB_45I_audio_P y-split
pdtype REFLWFSB_45Q_audio_P y-split

###########################################################################
 %%% FTend REFLWFS

noxaxis